// скрипт для тестирования стандартной библиотеки, клон https://github.com/EvilBeaver/oscript-library
// код будет работать, если 1testrunner как сабмодуль добавлен в библиотеку в подкаталог 1testrunner

Программа  = ОбъединитьПути(КаталогПрограммы(), "oscript.exe");
ЮнитТестер = ОбъединитьПути(ТекущийСценарий().Каталог, "1testrunner");
Библиотека = ОбъединитьПути(ТекущийСценарий().Каталог, "..\src");

СодержимоеБиблиотеки = НайтиФайлы(Библиотека, ПолучитьМаскуВсеФайлы());

Для Каждого ФайлБиблиотеки Из СодержимоеБиблиотеки Цикл
	
	Если ФайлБиблиотеки.ЭтоКаталог() Тогда
		Сообщить("Вход в каталог пакета " + ФайлБиблиотеки.Имя);
		
		КаталогТестов = Новый Файл(ОбъединитьПути(ФайлБиблиотеки.ПолноеИмя, "tests"));
		Если КаталогТестов.Существует() и КаталогТестов.ЭтоКаталог() Тогда
			
			ФайлТестовПакета = Новый Файл(ОбъединитьПути(КаталогТестов.ПолноеИмя, "tests.xml"));
			Если ФайлТестовПакета.Существует() Тогда
				Сообщить("Удаляю старый отчет о тестировании: " + ФайлТестовПакета.ПолноеИмя);
				УдалитьФайлы(ФайлТестовПакета.ПолноеИмя);
			КонецЕсли;
			
			Сообщить("Каталог тестов найден");
			
			СтрокаЗапуска = """" + Программа + """ """ + ЮнитТестер + """ -runall """ + КаталогТестов.ПолноеИмя + """ xddReportPath .";
			Сообщить("Запускаю: " + СтрокаЗапуска);
			
			Процесс = СоздатьПроцесс(СтрокаЗапуска, КаталогТестов.ПолноеИмя, Истина);
			Процесс.Запустить();
			Пока Не Процесс.Завершен Цикл
				Сообщить(Процесс.ПотокВывода.ПрочитатьСтроку());
			КонецЦикла;
			
			Сообщить(Процесс.ПотокВывода.Прочитать());
			
			Сообщить("Тестирование закончено");
			
			ФайлТестовПакета = Новый Файл(ОбъединитьПути(КаталогТестов.ПолноеИмя, "tests.xml"));
			Если ФайлТестовПакета.Существует() Тогда
				НовыйПуть = ОбъединитьПути(ТекущийСценарий().Каталог, ФайлБиблиотеки.Имя + ".xml");
				Сообщить("Копирую отчет о тестировании: " + НовыйПуть);
				КопироватьФайл(ФайлТестовПакета.ПолноеИмя, НовыйПуть);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецЦикла;